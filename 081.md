yixxas

medium

# Use of transfer and transferFrom is not recommended.

## Summary

OpenZeppelin recommends the use of safeTransfer and safeTransferFrom when doing ERC20 transfers whenever possible. 5 such instances are found in the contract.

## Vulnerability Detail

Some ERC20s are non-compliant and have different implementations of token transfer. For example, tokens such as ZRX do not revert on failure and instead returns a `false` value. Hence, it will cause deposit/withdrawal to silently fail.

## Impact

In our case, the current implementation of LINK token does revert on failure. But it is still better to err on the side of caution as future implementations may change this.

## Code Snippet

[Vault.sol#L131-L142](https://github.com/sherlock-audit/2022-10-mycelium/blob/main/mylink-contracts/src/Vault.sol#L131-L142)
```solidity
    function deposit(uint256 _amount) external {
        require(_amount > 0, "Amount must be greater than 0");
        require(_amount <= availableForDeposit(), "Amount exceeds available capacity");

        uint256 newShares = convertToShares(_amount);
        _mintShares(msg.sender, newShares);

        IERC20(LINK).transferFrom(msg.sender, address(this), _amount);
        _distributeToPlugins();

        emit Deposit(msg.sender, _amount);
    }
```
[Vault.sol#L153-L164](https://github.com/sherlock-audit/2022-10-mycelium/blob/main/mylink-contracts/src/Vault.sol#L153-L164)
```solidity
    function withdraw(uint256 _amount) external {
        require(_amount > 0, "Amount must be greater than 0");
        require(_amount <= balanceOf(msg.sender), "Amount exceeds balance");

        _ensureLinkAmount(_amount);

        _burnShares(msg.sender, convertToShares(_amount));

        IERC20(LINK).transfer(msg.sender, _amount);

        emit Withdraw(msg.sender, _amount);
    }
```
[Vault.sol#L174-L185](https://github.com/sherlock-audit/2022-10-mycelium/blob/main/mylink-contracts/src/Vault.sol#L174-L185)
```solidity
    function withdrawAll() external {
        uint256 amount = balanceOf(msg.sender);
        require(amount > 0, "Amount must be greater than 0");

        _ensureLinkAmount(amount);

        _burnShares(msg.sender, shares[msg.sender]);

        IERC20(LINK).transfer(msg.sender, amount);

        emit Withdraw(msg.sender, amount);
    }
```

[BasePlugin.sol#L42-L47](https://github.com/sherlock-audit/2022-10-mycelium/blob/main/mylink-contracts/src/plugins/BasePlugin.sol#L42-L47)
```solidity
    function deposit(uint256 _amount) external override onlyVault {
        require(_amount > 0, "Amount must be greater than 0");
        require(_amount <= availableForDeposit(), "Amount exceeds available capacity");
        IERC20(LINK).transferFrom(vault, address(this), _amount);
        _deposit(_amount);
    }
```

[BasePlugin.sol#L56-L60](https://github.com/sherlock-audit/2022-10-mycelium/blob/main/mylink-contracts/src/plugins/BasePlugin.sol#L56-L60)
```solidity
    function withdraw(uint256 _amount) external override onlyVault {
        require(_amount <= availableForWithdrawal(), "Amount exceeds available balance");
        _withdraw(_amount);
        IERC20(LINK).transfer(vault, _amount);
    }
```


## Tool used

Manual Review

## Recommendation

Use OpenZeppelin's SafeERC20 that implements `safeTransferFrom` and `safeTransfer`.
