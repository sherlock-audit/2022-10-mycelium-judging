Chom

high

# Withdraw convertToShares must be rounded up

## Summary
Withdraw convertToShares must be rounded up

## Vulnerability Detail
```solidity
 function withdraw(uint256 _amount) external { 
     require(_amount > 0, "Amount must be greater than 0"); 
     require(_amount <= balanceOf(msg.sender), "Amount exceeds balance"); 
  
     _ensureLinkAmount(_amount); 
  
     _burnShares(msg.sender, convertToShares(_amount)); 
  
     IERC20(LINK).transfer(msg.sender, _amount); 
  
     emit Withdraw(msg.sender, _amount); 
 }
```

Withdraw is using convertToShares

```solidity
     /** 
      * @notice Converts `_tokens` to an equivalent number of shares 
      * @dev Rounds down 
      * 
      * @return The number of shares equivalent to `_tokens` 
      * @param _tokens The number of tokens to convert 
      */ 
     function convertToShares(uint256 _tokens) public view returns (uint256) { 
         uint256 tokenSupply = totalSupply(); // saves one SLOAD 
         if (tokenSupply == 0) { 
             return _tokens * STARTING_SHARES_PER_LINK; 
         } 
         return _tokens.mulDivDown(totalShares, tokenSupply); 
     } 
```

convertToShares is rounding down

For example if 1 share = 10 assets

withdraw 5 assets will use 5/10 = 0 shares. The user can withdraw 5 assets for free. This is a loss of funds.

## Impact
Withdrawal may cause fund loss due to rounding

## Code Snippet

https://github.com/sherlock-audit/2022-10-mycelium/blob/main/mylink-contracts/src/Vault.sol#L607-L620

https://github.com/sherlock-audit/2022-10-mycelium/blob/main/mylink-contracts/src/Vault.sol#L153-L164

https://github.com/OpenZeppelin/openzeppelin-contracts/blob/8d908fe2c20503b05f888dd9f702e3fa6fa65840/contracts/token/ERC20/extensions/ERC4626.sol#L118-L120

https://github.com/OpenZeppelin/openzeppelin-contracts/blob/8d908fe2c20503b05f888dd9f702e3fa6fa65840/contracts/token/ERC20/extensions/ERC4626.sol#L148-L159

## Tool used

Compare with an official ERC4626 OpenZeppelin implementation

```solidity
    function previewWithdraw(uint256 assets) public view virtual override returns (uint256) {
        return _convertToShares(assets, Math.Rounding.Up);
    }

    /** @dev See {IERC4626-withdraw}. */
    function withdraw(
        uint256 assets,
        address receiver,
        address owner
    ) public virtual override returns (uint256) {
        require(assets <= maxWithdraw(owner), "ERC4626: withdraw more than max");

        uint256 shares = previewWithdraw(assets);
        _withdraw(_msgSender(), receiver, owner, assets, shares);

        return shares;
    }
```

Since we are converting assets to shares on withdraw, we should use previewWithdraw which is rounded up to calculate the share.

## Recommendation
`uint256 newShares = convertToShares(_amount);` must be rounded up instead of rounded down
