rbserver

high

# After `initialize` function is called and before `deposit` or `onTokenTransfer` function is called for the first time for a deployed vault, an attacker can directly transfer `1 wei` of LINK to the vault to force subsequent depositors to receive `0` myLINK shares and lose all deposited LINK amounts

## Summary
For a deployed vault, an attacker can directly transfer `1 wei` of LINK to the vault after the `initialize` function is called and before the `deposit` or `onTokenTransfer` function is called for the first time. Depositors, who subsequently deposit LINK to the vault, will receive `0` shares of myLINK and lose these deposited LINK amounts.

## Vulnerability Detail
The following steps can occur.
1. A vault is deployed.
2. The `initialize` function is called for the deployed vault.
3. Alice transfers `1 wei` of LINK directly to the vault by calling LINK's `transfer` function. At this moment, the corresponding `Vault` contract's `totalSupply` function returns `1`.
4. Bob deposits `1e18` LINK by calling the `deposit` function, which further calls the `convertToShares` function.
5. Calling the `convertToShares` function does not execute `return _tokens * STARTING_SHARES_PER_LINK` because `totalSupply()` returns `1`. Since `totalShares` is `0` at this moment, calling the `convertToShares` function returns `0`. Thus, Bob receives `0` shares.
6. Yet, because `IERC20(LINK).transferFrom(msg.sender, address(this), _amount)` is still executed, Bob has transferred `1e18` LINK to the vault.
7. Later, Eve calls LINK's `transferAndCall` function to transfer `2e18` LINK to the vault, which further calls the `onTokenTransfer` function.
8. When calling `onTokenTransfer`, `supplyBeforeTransfer` would be positive so `_amount.mulDivDown(totalShares, supplyBeforeTransfer)` would be executed. Since `totalShares` is still `0`, Eve would receive `0` shares as well.
9. Because Bob and Eve do not own any shares, calling the `withdraw` or `withdrawAll` function cannot transfer any LINK to them.

## Impact
Depositors lose all of their deposited LINK amounts because they do not own any shares and cannot withdraw any deposited amounts.

## Code Snippet
mylink-contracts/src/Vault.sol#L109-L120
```solidity
    function initialize(
        address _LINK,
        address _owner,
        uint256 _capacity,
        uint256 _startingSharesPerLink
    ) public initializer {
        LINK = _LINK;
        decimals = IERC20Metadata(_LINK).decimals();
        owner = _owner;
        MAX_CAPACITY = _capacity;
        STARTING_SHARES_PER_LINK = _startingSharesPerLink;
    }
```

mylink-contracts/src/Vault.sol#L552-L558
```solidity
    function totalSupply() public view override returns (uint256) {
        uint256 supply = IERC20(LINK).balanceOf(address(this));
        for (uint256 i = 0; i < pluginCount; i++) {
            supply += IPlugin(plugins[i]).balance();
        }
        return supply;
    }
```

mylink-contracts/src/Vault.sol#L131-L142
```solidity
    function deposit(uint256 _amount) external {
        require(_amount > 0, "Amount must be greater than 0");
        require(_amount <= availableForDeposit(), "Amount exceeds available capacity");

        uint256 newShares = convertToShares(_amount);
        _mintShares(msg.sender, newShares);

        IERC20(LINK).transferFrom(msg.sender, address(this), _amount);
        _distributeToPlugins();

        emit Deposit(msg.sender, _amount);
    }
```

mylink-contracts/src/Vault.sol#L614-L620
```solidity
    function convertToShares(uint256 _tokens) public view returns (uint256) {
        uint256 tokenSupply = totalSupply(); // saves one SLOAD
        if (tokenSupply == 0) {
            return _tokens * STARTING_SHARES_PER_LINK;
        }
        return _tokens.mulDivDown(totalShares, tokenSupply);
    }
```

mylink-contracts/src/Vault.sol#L252-L276
```solidity
    function onTokenTransfer(
        address _from,
        uint256 _amount,
        bytes memory _data
    ) external override returns (bool) {
        require(msg.sender == LINK, "Must use LINK token");
        require(keccak256(_data) == keccak256(abi.encodePacked("deposit")), "Data must be 'deposit'");

        require(_amount > 0, "Amount must be greater than 0");
        require(totalSupply() <= MAX_CAPACITY, "Amount exceeds available capacity");

        // We must calculate the shares based on the supply before the transfer
        uint256 supplyBeforeTransfer = totalSupply() - _amount;
        uint256 newShares = supplyBeforeTransfer == 0
            ? _amount * STARTING_SHARES_PER_LINK
            : _amount.mulDivDown(totalShares, supplyBeforeTransfer);

        _mintShares(_from, newShares);

        _distributeToPlugins();

        emit Deposit(_from, _amount);

        return true;
    }
```

## Tool used

Manual Review

## Recommendation
A state variable can be added to bookkeep the LINK amounts that are received by the vault through calling the `deposit` and `onTokenTransfer` functions but are not yet transferred to the plugins and depositors. Then, the `totalSupply` function can be updated to use this state variable, instead of `IERC20(LINK).balanceOf(address(this))`, to initialize `supply`.