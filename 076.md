hansfriese

high

# `Vault.convertToShares()` and `onTokenTransfer()` check the wrong condition for the first deposit.

## Summary
`Vault.convertToShares()` and `onTokenTransfer()` check the wrong condition for the first deposit.

## Vulnerability Detail
`Vault.convertToShares()` and `onTokenTransfer()` check the wrong condition for the first deposit.

These 2 functions check the wrong logic together and I will explain with `Vault.convertToShares()` only.

It checks if `tokenSupply == 0` for the first deposit [here](https://github.com/sherlock-audit/2022-10-mycelium/blob/main/mylink-contracts/src/Vault.sol#L616) but `tokenSupply` can be manipuldated easily.

```
File: 2022-10-mycelium-hansfriese\mylink-contracts\src\Vault.sol
614:     function convertToShares(uint256 _tokens) public view returns (uint256) {
615:         uint256 tokenSupply = totalSupply(); // saves one SLOAD
616:         if (tokenSupply == 0) {
617:             return _tokens * STARTING_SHARES_PER_LINK;
618:         }
619:         return _tokens.mulDivDown(totalShares, tokenSupply);
620:     }
```

This scenario would be possible.
- An owner deployed the vault contract.
- An attacker transfers 1 wei of `LINK` to the contract directly before any deposits.
- When a user deposits to this vault for the first time, `tokenSupply` will be equal or greater than 1 wei in `convertToShares()` as it's calculated below.

```
File: 2022-10-mycelium-hansfriese\mylink-contracts\src\Vault.sol
552:     function totalSupply() public view override returns (uint256) {
553:         uint256 supply = IERC20(LINK).balanceOf(address(this));
554:         for (uint256 i = 0; i < pluginCount; i++) {
555:             supply += IPlugin(plugins[i]).balance();
556:         }
557:         return supply;
558:     }
```
- A user will get 0 shares and it won't revert because `tokenSupply > 0`, `totalShares = 0` and `deposit()` function doesn't check `newShares != 0`. So `totalShares` will be 0 again.
- So all users will get 0 shares as well and lose their funds forever after that. 

## Impact
When an attacker transfers 1 wei of `LINK` before any deposits, all users will get 0 shares and lose their funds.

## Code Snippet
https://github.com/sherlock-audit/2022-10-mycelium/blob/main/mylink-contracts/src/Vault.sol#L614-L620

## Tool used
Solidity Visual Developer of VSCode

## Recommendation
We should check `totalShares == 0` instead of `tokenSupply == 0`.

```
function convertToShares(uint256 _tokens) public view returns (uint256) {
    uint256 tokenSupply = totalSupply(); // saves one SLOAD
    if (totalShares == 0) {
        return _tokens * STARTING_SHARES_PER_LINK;
    }
    return _tokens.mulDivDown(totalShares, tokenSupply);
}
```

```
function onTokenTransfer(
    address _from,
    uint256 _amount,
    bytes memory _data
) external override returns (bool) {
    require(msg.sender == LINK, "Must use LINK token");
    require(keccak256(_data) == keccak256(abi.encodePacked("deposit")), "Data must be 'deposit'");

    require(_amount > 0, "Amount must be greater than 0");
    require(totalSupply() <= MAX_CAPACITY, "Amount exceeds available capacity");

    // We must calculate the shares based on the supply before the transfer
    uint256 supplyBeforeTransfer = totalSupply() - _amount;
    uint256 newShares = totalShares == 0 //++++++++++++++++++++++++++++++++
        ? _amount * STARTING_SHARES_PER_LINK
        : _amount.mulDivDown(totalShares, supplyBeforeTransfer);

    _mintShares(_from, newShares);

    _distributeToPlugins();

    emit Deposit(_from, _amount);

    return true;
}
```