berndartmueller

medium

# Duplicate plugins cause multiple counting of `LINK` token balance

## Summary

The owner of the `Vault` contract can add duplicate plugins. This causes incorrect internal calculations in various functions and a state where vault deposits and withdrawals are negatively affected.

## Vulnerability Detail

The `Vault` contract owner can add plugins with the same address. The `Vault` contract keeps track of the number of plugins (`pluginCount`) and the addresses of the plugins (`plugins`). However, the `Vault.addPlugin` function does not check if a plugin is already registered, thus allowing the owner to add the same plugin multiple times.

To remove a duplicate plugin, the owner can use the `Vault.removePlugin` function and remove a plugin by its index. This will reset the `LINK` spending allowance of the plugin to 0, leaving the remaining plugin with the same address with an allowance of 0. Therefore, this will require removing and re-adding the plugin to have the proper allowance again.

## Impact

Duplicate registered plugins cause the `LINK` token balance to be counted multiple times. This leads to a wrong `totalSupply` calculation and wrong calculation within the functions `convertToTokens`, `convertToShares` and `availableForDeposit`. Additionally, the state where a registered plugin has a zero spending allowance for `LINK` tokens causes a temporary DoS of deposits and withdrawals.

## Code Snippet

[Vault.addPlugin](https://github.com/sherlock-audit/2022-10-mycelium/blob/main/mylink-contracts/src/Vault.sol#L314-L329)

```solidity
function addPlugin(address _plugin, uint256 _index) external onlyOwner {
    require(_plugin != address(0), "Cannot add zero address");
    require(_index <= pluginCount, "Index must be less than or equal to plugin count");

    uint256 pointer = pluginCount;
    while (pointer > _index) {
        plugins[pointer] = plugins[pointer - 1];
        pointer--;
    }
    plugins[pointer] = _plugin;
    pluginCount++;

    IERC20(LINK).approve(_plugin, type(uint256).max);

    emit PluginAdded(_plugin, _index);
}
```

[Vault.sol#L139](https://github.com/sherlock-audit/2022-10-mycelium/blob/main/mylink-contracts/src/Vault.sol#L139)

```solidity
function deposit(uint256 _amount) external {
    require(_amount > 0, "Amount must be greater than 0");
    require(_amount <= availableForDeposit(), "Amount exceeds available capacity");

    uint256 newShares = convertToShares(_amount);
    _mintShares(msg.sender, newShares);

    IERC20(LINK).transferFrom(msg.sender, address(this), _amount);
    _distributeToPlugins(); // @audit-info Transferring LINK tokens from the vault to the plugin with zero spending allowance will revert -> causing deposits to fail

    emit Deposit(msg.sender, _amount);
}
```

## Tools Used

Manual review

## Recommendation

Consider using a mapping to keep track of unique plugins.
