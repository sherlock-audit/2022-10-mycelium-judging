Ruhum

medium

# Adding same plugin twice can break vault's internal bookkeeping

## Summary
The Vault determines the total assets it controls by also checking the balance of each of its plugins. If the same plugin is added twice, its assets will also be counted twice. That will break the internal bookkeeping. For example, deposits will receive fewer shares.

## Vulnerability Detail
It's possible for the owner to add the same plugin twice. That will inflate the value of `totalSuplpy()` which is used to convert tokens into shares and determine whether the vault reached its maximum capacity or not. 

## Impact
Loss of funds for depositors because they get fewer shares than they should. Possible DOS if the maximum capacity is surpassed because of the inflated value.

## Code Snippet
https://github.com/sherlock-audit/2022-10-mycelium/blob/main/mylink-contracts/src/Vault.sol#L314-L329

No checks whether the plugin already exists in the array or not.
```sol
    function addPlugin(address _plugin, uint256 _index) external onlyOwner {
        require(_plugin != address(0), "Cannot add zero address");
        require(_index <= pluginCount, "Index must be less than or equal to plugin count");

        uint256 pointer = pluginCount;
        while (pointer > _index) {
            plugins[pointer] = plugins[pointer - 1];
            pointer--;
        }
        plugins[pointer] = _plugin;
        pluginCount++;

        IERC20(LINK).approve(_plugin, type(uint256).max);

        emit PluginAdded(_plugin, _index);
    }
```

`totalSupply()` uses the plugin's balance:
```sol
    function totalSupply() public view override returns (uint256) {
        uint256 supply = IERC20(LINK).balanceOf(address(this));
        for (uint256 i = 0; i < pluginCount; i++) {
            supply += IPlugin(plugins[i]).balance();
        }
        return supply;
    }
```

And `totalSupply()` is used to get convert incoming assets into shares:
```sol
    function convertToShares(uint256 _tokens) public view returns (uint256) {
        uint256 tokenSupply = totalSupply(); // saves one SLOAD
        if (tokenSupply == 0) {
            return _tokens * STARTING_SHARES_PER_LINK;
        }
        return _tokens.mulDivDown(totalShares, tokenSupply);
    }
```

## Tool used

Manual Review

## Recommendation
When adding a new plugin check whether it already exists in the array. Since this is a function only called by the owner, the extra gas costs should be irrelevant.