berndartmueller

medium

# A single faulty or misbehaving plugin can cause withdrawals to fail and not be able to remove the affected plugin

## Summary

If a plugin is faulty and reverts on withdrawals, the vault withdrawal functions may revert and the plugin can not be removed.

## Vulnerability Detail

The `Vault.withdraw` and the `Vault.withdrawAll` functions call the `Vault._ensureLinkAmount` function internally. This function ensures the vault has enough `LINK` to cover the withdrawal and withdraws `LINK` tokens accordingly from the plugins. However, in case of a faulty and reverting plugin, the `Vault._ensureLinkAmount` function will revert (the `Vault._withdrawFromPlugin` reverts, to be precise) and the vault contract will not be able to handle withdrawals as well as removing the faulty plugin.

## Impact

A single faulty plugin that reverts on withdrawals can cause the whole vault to be stuck withdrawing funds. The affected plugin can not be removed by the contract owner as long as the issue persists.

## Code Snippet

Both the `Vault.withdraw` and the `Vault.withdrawAll` function call the `Vault._ensureLinkAmount` function. This function ensures the vault has enough `LINK` to cover the withdrawal and withdraws `LINK` funds accordingly from the plugins.

[Vault.withdraw](https://github.com/sherlock-audit/2022-10-mycelium/blob/main/mylink-contracts/src/Vault.sol#L153-L164)

```solidity
function withdraw(uint256 _amount) external {
    require(_amount > 0, "Amount must be greater than 0");
    require(_amount <= balanceOf(msg.sender), "Amount exceeds balance");

    _ensureLinkAmount(_amount);

    _burnShares(msg.sender, convertToShares(_amount));

    IERC20(LINK).transfer(msg.sender, _amount);

    emit Withdraw(msg.sender, _amount);
}
```

[Vault.withdrawAll](https://github.com/sherlock-audit/2022-10-mycelium/blob/main/mylink-contracts/src/Vault.sol#L174-L185)

```solidity
function withdrawAll() external {
    uint256 amount = balanceOf(msg.sender);
    require(amount > 0, "Amount must be greater than 0");

    _ensureLinkAmount(amount);

    _burnShares(msg.sender, shares[msg.sender]);

    IERC20(LINK).transfer(msg.sender, amount);

    emit Withdraw(msg.sender, amount);
}
```

The `Vault._ensureLinkAmount` withdraws `LINK` from a plugin via the `Vault._withdrawFromPlugin` function.

[Vault.\_ensureLinkAmount](https://github.com/sherlock-audit/2022-10-mycelium/blob/main/mylink-contracts/src/Vault.sol#L492-L519)

```solidity
function _ensureLinkAmount(uint256 _requested) internal {
    require(_requested <= availableForWithdrawal(), "Amount exceeds available balance");

    uint256 currentBalance = IERC20(LINK).balanceOf(address(this));
    if (currentBalance >= _requested) {
        return;
    }

    uint256 remaining = _requested - currentBalance;
    // Withdraw in reverse order of deposit
    for (uint256 i = 0; i < pluginCount; i++) {
        if (remaining == 0) {
            break;
        }

        address plugin = plugins[pluginCount - i - 1];
        uint256 available = IPlugin(plugin).availableForWithdrawal();
        if (available > 0) {
            uint256 amount = available > remaining ? remaining : available;
            _withdrawFromPlugin(plugin, amount);
            remaining -= amount;
        }
    }

    if (remaining > 0) {
        revert("Unable to withdraw enough LINK from plugins");
    }
}
```

[Vault.\_withdrawFromPlugin](https://github.com/sherlock-audit/2022-10-mycelium/blob/main/mylink-contracts/src/Vault.sol#L528)

```solidity
function _withdrawFromPlugin(address _plugin, uint256 _amount) internal {
    IPlugin(_plugin).withdraw(_amount);
}
```

However, a plugin may misbehave and withdrawals revert for any reason (e.g., blacklisted address due to a dusted Tornado cash transaction, temporarily paused, etc.). In this case, the `Vault._ensureLinkAmount` function will revert and the `Vault.withdraw` or `Vault.withdrawAll` function will revert as well.

Additionally, the affected plugin can possibly not be removed from the vault because the `Vault.removePlugin` function tries to withdraw funds via the plugin's `IPlugin.withdraw` function.

[Vault.removePlugin](https://github.com/sherlock-audit/2022-10-mycelium/blob/main/mylink-contracts/src/Vault.sol#L345)

```solidity
function removePlugin(uint256 _index) external onlyOwner {
    require(_index < pluginCount, "Index out of bounds");
    address pluginAddr = plugins[_index];

    _withdrawFromPlugin(pluginAddr, IPlugin(pluginAddr).balance());

    uint256 pointer = _index;
    while (pointer < pluginCount - 1) {
        plugins[pointer] = plugins[pointer + 1];
        pointer++;
    }
    delete plugins[pluginCount - 1];
    pluginCount--;

    IERC20(LINK).approve(pluginAddr, 0);

    emit PluginRemoved(pluginAddr);
}
```

## Tools Used

Manual review

## Recommendation

Consider adding an additional function to remove a plugin in emergency situations without withdrawing funds from the plugin.
