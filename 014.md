ctf_sec

high

# totalSupply() can be maliciously inflated and manipulated, User may lose their token and not get share minted  in Vault.sol#deposit when convertToShares return 0

## Summary

User may deposit token and not get any shares because minted share can be 0 because totalSupply() can be maliciously inflated and convertToShares return 0, then users lose the token they deposited in Vault.sol#deposit

## Vulnerability Detail

the function deposit in the Vault.sol preview the share then mint the share to the user

```solidity
         uint256 newShares = convertToShares(_amount);
        _mintShares(msg.sender, newShares);
        IERC20(LINK).transferFrom(msg.sender, address(this), _amount)
```

however, the convertToShares(_amount) can return 0, if 0 is returned,

then 

```solidity
 _mintShares(msg.sender, newShares);
```

mint nothing, but LINK token is still transfered from user address to the vault.sol.

Let me use the POC to demonstrate that convertToShares can return 0 bause the totalSupply() can be malicious inflated.

the function convertToShares use the formula:

token amount * total shares / total supply , if total supply is a very large number, it can return 0.

```solidity
    function convertToShares(uint256 _tokens) public view returns (uint256) {
        uint256 tokenSupply = totalSupply(); // saves one SLOAD
        if (tokenSupply == 0) {
            return _tokens * STARTING_SHARES_PER_LINK;
        }
        return _tokens.mulDivDown(totalShares, tokenSupply);
    }
```

the total supply is the sum of the link balance in the contract and the aLink token balance in all vault.

```solidity
    function totalSupply() public view override returns (uint256) {
        uint256 supply = IERC20(LINK).balanceOf(address(this));
        for (uint256 i = 0; i < pluginCount; i++) {
            supply += IPlugin(plugins[i]).balance();
        }
        return supply;
    }
```

Given the above implementation, total supply can be inflated in three ways.

1. Malicious actor can forcefully send LINK token to the vault contract to inflate the total supply.
2. Malicious actor can forcefully send aLINK token to the plugin contract to inflate total supply.
3. Malicious actor call deposit function directly on the AAVE V2 lending pool and deposit on behalf the plugin, then aLink token is minted directly toward the plugin contract.

https://github.com/aave/protocol-v2/blob/554a2ed7ca4b3565e2ceaea0c454e5a70b3a2b41/contracts/protocol/lendingpool/LendingPool.sol#L105

```solidity
  function deposit(
    address asset,
    uint256 amount,
    address onBehalfOf,
    uint16 referralCode
  ) external override whenNotPaused {
```

https://github.com/aave/protocol-v2/blob/554a2ed7ca4b3565e2ceaea0c454e5a70b3a2b41/contracts/protocol/lendingpool/LendingPool.sol#L122

```solidity
bool isFirstDeposit = IAToken(aToken).mint(onBehalfOf, amount, reserve.liquidityIndex);
```

## Impact

User may deposit token and not get any shares, then they lose the token they deposited in Vault.sol#deposit

# POC

the POC below shows how convertToShares function return when the total supply is inflated.

```solidity
    function testZeroShare_POC() public {
    
        uint256 amount = 1 ether;

        // alice deposit 1 ETHER amount of link
        vm.startPrank(alice);
        link.mint(alice,  amount);
        link.approve(address(vault), type(uint256).max);
        vault.deposit(amount);
        vm.stopPrank();

        uint256 sharesAmount = vault.convertToShares(0.5 ether);
        console.log("normally how many shares amount is returned");
        console.log(sharesAmount);
        console.log("------------------");

        link.mint(address(vault), 10000 ether);
        sharesAmount = vault.convertToShares(0.5 ether);
        console.log("less shares amount is returned when total supply is inflated");
        console.log(sharesAmount);
        console.log("------------------");

        link.mint(address(vault), 1000000000000000000000000 ether);
        console.log("0 shares amount is returned when total supply is inflated to a large number");
        sharesAmount = vault.convertToShares(0.5 ether);
        console.log(sharesAmount);

    }
```

please add the POC to the file deposit.t.sol under test folder

https://github.com/sherlock-audit/2022-10-mycelium/blob/main/mylink-contracts/test/Vault/deposit.t.sol

we can run the contest by running the command

```solidity
forge test -vvv --match testZeroShare_POC
```

and the running result is 

Running 1 test for test/Vault/deposit.t.sol:Vault_depositTest
[PASS] testZeroShare_POC() (gas: 180904)
Logs:
  normally how many shares amount is returned
  500000000000000000000
  ------------------
  less shares amount is returned when total supply is inflated
  49995000499950004
  ------------------
  0 shares amount is returned when total supply is inflated to a large number
  0

## Code Snippet

https://github.com/sherlock-audit/2022-10-mycelium/blob/main/mylink-contracts/src/Vault.sol#L131-L142

https://github.com/sherlock-audit/2022-10-mycelium/blob/main/mylink-contracts/src/Vault.sol#L614-L621

https://github.com/sherlock-audit/2022-10-mycelium/blob/main/mylink-contracts/src/Vault.sol#L552-L558

## Tool used

Manual Review

## Recommendation

We recommend the user add check to make sure 0 share is not allowed when user deposit token.

We also recommend the project take a snapshot of the total supply and not use the balanceOf to calculate the total supply directly to avoid balance inflation.

```solidity   
        uint256 newShares = convertToShares(_amount);
         require(newShares > 0, 'invalid share');
        _mintShares(msg.sender, newShares);
        IERC20(LINK).transferFrom(msg.sender, address(this), _amount);
```