dipp

high

# Sending LINK to the contract before the first deposit could lead to no shares returned for deposits

## Summary

Due to the intial calculation of shares for deposits only considering when the amount of LINK controlled by the contract is 0 and not checking if the totalShares is 0, deposits might not return any shares if the contract initially held LINK.

## Vulnerability Detail

When depositing LINK to the vault using the ```deposit``` or ```onTokenTransfer``` functions the ```convertToShares``` function is used to determine the amount of shares to mint to the depositor. If the vault does not control any LINK (no LINK in contract and no LINK deposited to plugins) then the shares returned are calculated as ```_tokens * STARTING_SHARES_PER_LINK```. However, if the vault or plugins contain any amount of LINK then the shares will be calculated as ```tokens.mulDivDown(totalShares, tokenSupply)```. When shares are minted for the first time the ```totalShares``` are 0 which means the latter calculations would return 0 shares for any deposit amount.

Test code added to ```deposit.t.sol```:

```solidity
    function testCanReceiveNoShares(uint256 amount) public {
        vm.assume(amount > 1000000);
        vm.assume(amount < vault.MAX_CAPACITY()/2);

        link.mint(address(this), amount*2);
        link.approve(address(vault), amount*2);
        
        // Send link directly to vault without minting shares
        link.transfer(address(vault), 1);

        vault.deposit(amount - 1);                                 // At this point tokenSupply == 1 but totalShares == 0.

        assertEq(vault.totalSupply(), amount);
        assertEq(vault.balanceOf(address(this)), 0);               // Sent all link to the vault
        assertEq(vault.shares(address(this)), 0);                  // User receives no shares on first deposit

        // Deposit again
        vault.deposit(amount);

        assertEq(vault.totalSupply(), amount*2);
        assertEq(vault.balanceOf(address(this)), 0);               // Sent all link to the vault
        assertEq(vault.shares(address(this)), 0);                  // User receives no shares on second deposit
    }
```

The test code above shows the contract sending 1 LINK to the vault then calling its ```deposit``` function with a non-zero amount. Afterwards, it is shown that the amount of shares gained by the contract (user) is 0. Since no shares are minted, the second deposit (and deposits thereafter) will also return 0 shares.

## Impact

Since ```balanceOf``` will return 0 due to the user's myLINK share balance being 0, users would be unable to withdraw their deposited LINK.

## Code Snippet

[Vault.sol:convertToShares](https://github.com/sherlock-audit/2022-10-mycelium/blob/main/mylink-contracts/src/Vault.sol#L614-L620)
```solidity
    function convertToShares(uint256 _tokens) public view returns (uint256) {
        uint256 tokenSupply = totalSupply(); // saves one SLOAD
        if (tokenSupply == 0) {
            return _tokens * STARTING_SHARES_PER_LINK;
        }
        return _tokens.mulDivDown(totalShares, tokenSupply);
    }
```

[Vault.sol:withdraw](https://github.com/sherlock-audit/2022-10-mycelium/blob/main/mylink-contracts/src/Vault.sol#L153-L164)
```solidity
    function withdraw(uint256 _amount) external {
        require(_amount > 0, "Amount must be greater than 0");
        require(_amount <= balanceOf(msg.sender), "Amount exceeds balance");

        _ensureLinkAmount(_amount);

        _burnShares(msg.sender, convertToShares(_amount));

        IERC20(LINK).transfer(msg.sender, _amount);

        emit Withdraw(msg.sender, _amount);
    }
```

[Vault.sol:balanceOf](https://github.com/sherlock-audit/2022-10-mycelium/blob/main/mylink-contracts/src/Vault.sol#L588-L590)
```solidity
    function balanceOf(address _account) public view override returns (uint256) {
        return convertToTokens(shares[_account]);
    }
```


[Vault.sol:convertToTokens](https://github.com/sherlock-audit/2022-10-mycelium/blob/main/mylink-contracts/src/Vault.sol#L599-L605)
```solidity
    function convertToTokens(uint256 _shares) public view returns (uint256) {
        uint256 shareSupply = totalShares; // saves one SLOAD
        if (shareSupply == 0) {
            return _shares / STARTING_SHARES_PER_LINK;
        }
        return _shares.mulDivDown(totalSupply(), shareSupply);
    }
```

## Tool used

Manual Review

## Recommendation

In the ```convertToShares``` function, consider adding a condition for when the ```totalShares``` == 0 so that a user does not receive 0 shares for a deposit..