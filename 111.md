dipp

medium

# Can have same plugin multiple times leading to inflated totalSupply

## Summary

The totalSupply (total amount of LINK in the vault contract and in the plugins) could be inflated if a plugin is added multiple times due to the balance of a plugin being accounted for multiple times in the ```totalSupply``` function.

## Vulnerability Detail

The ```totalSupply``` function in ```Vault.sol``` returns the total amount of LINK tokens held in the vault and in the plugins. The ```plugins``` mapping maps an integer to an address for a plugin so the same plugin may appear multiple times. If the same plugin is included multiple times then the totalSupply function will add its balance to the total supply multiple times thus returning an inflated amount of tokens.

Test code added to ```addPlugin.t.sol```:

```solidity
function testAddSamePlugin() public {
        // Deposit to increase plugins link supply
        link.mint(address(this), 1000000);
        link.approve(address(vault), 1000000);
        vault.deposit(1000000);

        // Check tokens distributed to plugin
        assertEq(link.balanceOf(address(vault)), 0);
        assertEq(plugin.balance(), 1000000);                    // 1000000 link gets sent to plugin
        assertEq(vault.totalSupply(), 1000000);

        assertEq(vault.balanceOf(address(this)), 1000000);     // The user (this contract) is entitled to 1000000 tokens

        // Add the same plugin
        vault.addPlugin(address(plugin), 1);

        // Check the vault has the same plugin twice
        assertEq(vault.plugins(0), address(plugin));
        assertEq(vault.plugins(1), address(plugin));
        assertEq(vault.pluginCount(), 2);

        // Check inflated tokenSupply
        assertEq(vault.totalSupply(), 2000000);                // Doubled totalSupply, plugin's link supply counted twice

        // Check the user is now entitled to twice as much tokens without having sent more
        assertEq(vault.balanceOf(address(this)), 2000000);
    }
```

In the test code above, tokens are deposited into the vault which are then distributed to the plugin intially added to the vault. The same plugin is then added again. The vault now has the same plugin twice and the totalSupply, and user's balance, has doubled without any transfer of tokens.

## Impact

The main functions affected are ```convertToTokens``` and ```convertToShares```. The ```convertToTokens``` will return a larger amount of tokens than it should which would allow users to withdraw mre than they are entitled to and the ```convertToShares``` will return less shares which would result in users receiving less shares for deposits.

## Code Snippet

[Vault.sol:addPlugin](https://github.com/sherlock-audit/2022-10-mycelium/blob/main/mylink-contracts/src/Vault.sol#L314-L329)
```solidity
    function addPlugin(address _plugin, uint256 _index) external onlyOwner {
        require(_plugin != address(0), "Cannot add zero address");
        require(_index <= pluginCount, "Index must be less than or equal to plugin count");

        uint256 pointer = pluginCount;
        while (pointer > _index) {
            plugins[pointer] = plugins[pointer - 1];
            pointer--;
        }
        plugins[pointer] = _plugin;
        pluginCount++;

        IERC20(LINK).approve(_plugin, type(uint256).max);

        emit PluginAdded(_plugin, _index);
    }
```

[Vault.sol:totalSupply](https://github.com/sherlock-audit/2022-10-mycelium/blob/main/mylink-contracts/src/Vault.sol#L552-L558)
```solidity
    function totalSupply() public view override returns (uint256) {
        uint256 supply = IERC20(LINK).balanceOf(address(this));
        for (uint256 i = 0; i < pluginCount; i++) {
            supply += IPlugin(plugins[i]).balance();
        }
        return supply;
    }
```

## Tool used

Manual Review

## Recommendation

Do not allow duplicate plugins. This could be done by using an additional mapping which returns true if a plugin has been added and false if it is no longer used. A plugin should be set to true in the ```addPlugin``` function and to false when removed in the ```removePlugin``` function.

Suggested code changes:
```solidity
    function addPlugin(address _plugin, uint256 _index) external onlyOwner {
        require(_plugin != address(0), "Cannot add zero address");
        require(_index <= pluginCount, "Index must be less than or equal to plugin count");
        require(usedPlugins[_plugin] == false, "Plugin has been added");

        uint256 pointer = pluginCount;
        while (pointer > _index) {
            plugins[pointer] = plugins[pointer - 1];
            pointer--;
        }
        plugins[pointer] = _plugin;
        pluginCount++;
        usedPlugins[_plugin] = true;

        IERC20(LINK).approve(_plugin, type(uint256).max);

        emit PluginAdded(_plugin, _index);
    }
```

```solidity
    function removePlugin(uint256 _index) external onlyOwner {
        require(_index < pluginCount, "Index out of bounds");
        address pluginAddr = plugins[_index];

        _withdrawFromPlugin(pluginAddr, IPlugin(pluginAddr).balance());

        uint256 pointer = _index;
        while (pointer < pluginCount - 1) {
            plugins[pointer] = plugins[pointer + 1];
            pointer++;
        }
        usedPlugins[plugin[pluginCount - 1]] = false;
        delete plugins[pluginCount - 1];
        pluginCount--;

        IERC20(LINK).approve(pluginAddr, 0);

        emit PluginRemoved(pluginAddr);
    }
```