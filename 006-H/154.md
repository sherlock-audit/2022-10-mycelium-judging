berndartmueller

high

# A paused Aave lending pool will cause a serious denial of service

## Summary

The Aave lending pool used by the `AaveV2Plugin` plugin can be paused. This prevents vault deposits, withdrawals, plugin rebalances, and removing the affected plugin.

## Vulnerability Detail

If the Aave lending pool is paused, the `AaveV2Plugin` plugin will not be able to deposit or withdraw funds from the lending pool. This is caused by the `AaveV2Plugin` plugin not checking the status of the lending pool and returning zero values for the available deposit and withdrawal amounts in case the lending pool is paused. The `Vault` contract tries to deposit or withdraw funds via the Aave plugin, but the plugin will revert.

## Impact

Vault deposits, withdrawals and plugin rebalance as well as removing the affected plugin with the paused Aave lending pool will revert. Funds will be stuck for as long as the Aave lending pool is paused.
This affects the whole vault and all its registered plugins.

## Code Snippet

The `AaveV2Plugin.availableForDeposit` and the `AaveV2Plugin.availableForWithdrawal` functions do not check if the Aave lending pool is currently paused.

[plugins/AaveV2Plugin.availableForDeposit](https://github.com/sherlock-audit/2022-10-mycelium/blob/main/mylink-contracts/src/plugins/AaveV2Plugin.sol#L72-L74)

```solidity
function availableForDeposit() public view override returns (uint256) {
    return type(uint256).max - balance();
}
```

[plugins/AaveV2Plugin.availableForWithdrawal](https://github.com/sherlock-audit/2022-10-mycelium/blob/main/mylink-contracts/src/plugins/AaveV2Plugin.sol#L82-L84)

```solidity
function availableForWithdrawal() public view override returns (uint256) {
    return balance();
}
```

[plugins/AaveV2Plugin.balance](https://github.com/sherlock-audit/2022-10-mycelium/blob/main/mylink-contracts/src/plugins/AaveV2Plugin.sol#L62-L64)

```solidity
function balance() public view override returns (uint256) {
    return aLINK.balanceOf(address(this));
}
```

The `Vault.deposit`, `Vault.rebalancePlugins` and the `Vault.onTokenTransfer` functions are calling the `Vault._distributeToPlugins` function internally. This function deposits the funds in the plugins and checks the amount of funds available to deposit by calling `IPlugin.availableForDeposit`. The plugin is skipped without depositing funds if the available amount is zero. However, in the case of the `AaveV2Plugin` plugin, if the Aave lending pool is paused, the `availableForDeposit` function will return a non-zero value, even though the plugin is not able to handle deposits. This will cause the aforementioned functions to revert.

[Vault.\_distributeToPlugins](https://github.com/sherlock-audit/2022-10-mycelium/blob/main/mylink-contracts/src/Vault.sol#L466)

```solidity
function _distributeToPlugins() internal {
    uint256 remaining = IERC20(LINK).balanceOf(address(this));

    // Plugins are ordered by priority. Fill the first one first, then the second, etc.
    for (uint256 i = 0; i < pluginCount; i++) {
        if (remaining == 0) {
            break;
        }

        address plugin = plugins[i];
        uint256 available = IPlugin(plugin).availableForDeposit();
        if (available > 0) {
            uint256 amount = available > remaining ? remaining : available;
            _depositToPlugin(plugin, amount);
            remaining -= amount;
        }
    }
}
```

Additionally, the plugin with the paused Aave lending pool can not be removed from the vault, because the `Vault.removePlugin` function tries to withdraw funds via the plugin's `IPlugin.withdraw` function. If the Aave lending pool is paused, the `IPlugin.withdraw` function will revert due to `ILendingPool.withdraw` reverting.

[Vault.removePlugin](https://github.com/sherlock-audit/2022-10-mycelium/blob/main/mylink-contracts/src/Vault.sol#L345)

```solidity
function removePlugin(uint256 _index) external onlyOwner {
    require(_index < pluginCount, "Index out of bounds");
    address pluginAddr = plugins[_index];

    _withdrawFromPlugin(pluginAddr, IPlugin(pluginAddr).balance());

    uint256 pointer = _index;
    while (pointer < pluginCount - 1) {
        plugins[pointer] = plugins[pointer + 1];
        pointer++;
    }
    delete plugins[pluginCount - 1];
    pluginCount--;

    IERC20(LINK).approve(pluginAddr, 0);

    emit PluginRemoved(pluginAddr);
}
```

## Tools Used

Manual review

## Recommendation

Consider checking in the `AaveV2Plugin.availableForDeposit` and `AaveV2Plugin.availableForWithdrawal` functions if the Aave lending pool is currently paused and return `0` in this case. Additionally, check the available amount for withdrawal in the `Vault._withdrawFromPlugin` is non-zero before calling `IPlugin.withdraw`.
