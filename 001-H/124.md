rbserver

high

# First depositor can manipulate share price and steal next depositor's deposited LINK amount

## Summary
The first depositor, who deposits LINK into the vault for minting shares of myLINK, can manipulate the share price through depositing `1 wei` of LINK and then transferring a large amount of LINK directly to the `Vault` contract. Due to the manipulated share price and precision loss, part or all of the next depositor's deposited LINK amount can be stolen by the first depositor.

## Vulnerability Detail
The following steps can occur for the case when the `deposit` function is called. The case when the `onTokenTransfer` function is called is similar to this.
1. Alice deposits `1 wei` of LINK by calling the `deposit` function, which further calls the `convertToShares` function.
2. Alice receives `STARTING_SHARES_PER_LINK`, which can be configured to `1000`, shares.
3. Alice transfers `10000e18` LINK directly to the `Vault` contract by calling LINK's `transfer` function. Now, the `Vault` contract's `totalSupply` function returns `10000e18 + 1`.
4. Bob deposits `10e18` LINK by calling the `deposit` function.
5. However, Bob receives `0` shares after the `convertToShares` function is called.
6. Yet, Bob still transfers `10e18` LINK to the `Vault` contract because `IERC20(LINK).transferFrom(msg.sender, address(this), _amount)` is executed.
7. Alice can call the `withdrawAll` function to withdraw her and Bob's deposited LINK amounts to herself because she owns all shares at this moment.
8. If, in Step 4, Bob deposits `10010e18` LINK, he would only receive `1000` shares, which is same as Alice's number of shares. Then, after calling `withdrawAll`, Alice is able to withdraw about `5e18` from Bob's deposited LINK amount.

## Impact
The next depositor loses part or all of her or his deposited LINK amount to the first depositor.

## Code Snippet

mylink-contracts/src/Vault.sol#L131-L142
```solidity
    function deposit(uint256 _amount) external {
        require(_amount > 0, "Amount must be greater than 0");
        require(_amount <= availableForDeposit(), "Amount exceeds available capacity");

        uint256 newShares = convertToShares(_amount);
        _mintShares(msg.sender, newShares);

        IERC20(LINK).transferFrom(msg.sender, address(this), _amount);
        _distributeToPlugins();

        emit Deposit(msg.sender, _amount);
    }
```

mylink-contracts/src/Vault.sol#L614-L620
```solidity
    function convertToShares(uint256 _tokens) public view returns (uint256) {
        uint256 tokenSupply = totalSupply(); // saves one SLOAD
        if (tokenSupply == 0) {
            return _tokens * STARTING_SHARES_PER_LINK;
        }
        return _tokens.mulDivDown(totalShares, tokenSupply);
    }
```

mylink-contracts/src/Vault.sol#L552-L558
```solidity
    function totalSupply() public view override returns (uint256) {
        uint256 supply = IERC20(LINK).balanceOf(address(this));
        for (uint256 i = 0; i < pluginCount; i++) {
            supply += IPlugin(plugins[i]).balance();
        }
        return supply;
    }
```

mylink-contracts/src/Vault.sol#L252-L276
```solidity
    function onTokenTransfer(
        address _from,
        uint256 _amount,
        bytes memory _data
    ) external override returns (bool) {
        require(msg.sender == LINK, "Must use LINK token");
        require(keccak256(_data) == keccak256(abi.encodePacked("deposit")), "Data must be 'deposit'");

        require(_amount > 0, "Amount must be greater than 0");
        require(totalSupply() <= MAX_CAPACITY, "Amount exceeds available capacity");

        // We must calculate the shares based on the supply before the transfer
        uint256 supplyBeforeTransfer = totalSupply() - _amount;
        uint256 newShares = supplyBeforeTransfer == 0
            ? _amount * STARTING_SHARES_PER_LINK
            : _amount.mulDivDown(totalShares, supplyBeforeTransfer);

        _mintShares(_from, newShares);

        _distributeToPlugins();

        emit Deposit(_from, _amount);

        return true;
    }
```

## Tool used

Manual Review

## Recommendation
When the first deposit occurs, a pre-determined minimum number of myLINK shares can be minted to `address(0)` before minting shares to the first depositor, which is similar to [Uniswap V2's implementation](https://github.com/Uniswap/v2-core/blob/master/contracts/UniswapV2Pair.sol#L119-L121).